plugins {
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
    id 'eclipse'
    id 'idea'
}

group = 'org.monospark'
version = '1.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    isCiServer = System.getenv().containsKey('JENKINS_HOME')
    isSnapshot = version.endsWith('SNAPSHOT')
}

repositories {
    mavenCentral()
	maven {
		url "http://repo.spongepowered.org/maven/"
	}
}

configurations {
    customCheckstyle
}

dependencies {
	compile 'org.spongepowered:spongeapi:4.0.3'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.10.19'
    customCheckstyle 'com.puppycrawl.tools:checkstyle:6.13'
}

checkstyle {
    //Only run checkstyle tests for main
    sourceSets = Collections.singleton(sourceSets.main)
    //Checkstyle config is not located at the default location, so it has to be changed
    configFile = file('checkstyle/checkstyle.xml')
    configProperties = [
        'basedir': project.projectDir,
    ]
}

tasks.checkstyleMain {
    //Use a custom version of checkstyle
    checkstyleClasspath = files(configurations.customCheckstyle.files)
}

//Include the license file in the jar
jar {
    from 'LICENSE.txt'
    rename 'LICENSE.txt', 'spongematchers-license.txt'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

if(isCiServer) {
    apply from: 'publish.gradle'
}
